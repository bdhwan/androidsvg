import java.text.SimpleDateFormat

apply plugin: 'com.android.library'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 24
        versionCode gitVersion()
        versionName genVersionName()

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}





def gitVersion() {
    def process = "git rev-list master --first-parent --count".execute()
    def result = process.text.trim()
    if (result == "") {
        return 1
    } else {
        return result.toInteger()
    }
}
def gitSting() {
    def process = "git rev-parse --short HEAD".execute()
    def result = process.text.toString().trim()
    if (result == "") {
        result = "no_git"
    }
    return result
}

def buildTime() {
    def df = new SimpleDateFormat("yyyyMMdd")
    return df.format(new Date())
}


def genVersionName() {
    return buildTime() + "_" + project.getRootProject().name + "_v" + gitVersion() + "." + gitSting()
}


task makeJar(type: Copy) {
    from('build/intermediates/bundles/release/')
    into('release/')
    include('classes.jar')
    rename ('classes.jar', "androidsvg_v_"+ gitVersion()+'.jar')

}

task clearJar(type: Delete) {
    delete fileTree(dir:'release',include:['*.jar'])
}


makeJar.dependsOn(clearJar, build)
